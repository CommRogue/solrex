plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	// optional graalvm
	//id 'org.graalvm.buildtools.native' version '0.10.2'
	id "com.diffplug.spotless" version "7.0.0.BETA3"
}

group = 'com.commrogue'
version = '0.0.1-SNAPSHOT'

spotless {
	ratchetFrom('origin/main')
	format 'misc', {
		target '*.gradle', '.gitattributes', '.gitignore'
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		importOrder()
		palantirJavaFormat('2.50.0')
		removeUnusedImports()
		formatAnnotations()
		licenseHeader '/* (C)Team Eclipse $YEAR */'
	}
}

afterEvaluate {
	def spotless = tasks.findByName('spotlessApply')
	if (spotless) {
		tasks.withType(JavaCompile) {
			finalizedBy(spotless)
		}

		tasks.withType(GroovyCompile) {
			finalizedBy(spotless)
		}
	}
}

java {
	toolchain {
		// Kus Ima Shel Palantir Java, it doesn't work with Java 22
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.5.0'
	implementation 'org.springframework.boot:spring-boot-starter-validation:3.3.0'
	implementation 'org.apache.solr:solr-solrj:9.6.1'
	implementation "io.ino:solrs_2.13:2.11.1"
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
}

tasks.named('test') {
	useJUnitPlatform()
}
